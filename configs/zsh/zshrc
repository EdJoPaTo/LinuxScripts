uptime

if [[ -d "/usr/share/zsh/plugins" ]]; then
	# probably Arch Linux
	ZSH_PLUGINS=/usr/share/zsh/plugins
else
	# probably macOS
	ZSH_PLUGINS=/usr/local/share
fi

if [[ -d "/usr/local/share/zsh-completions" ]]; then
	# macOS
	fpath+=/usr/local/share/zsh-completions
fi

# History
HISTSIZE=10000
SAVEHIST=4000
HISTFILE=~/.cache/zsh_history
# History options: man zshoptions
setopt EXTENDED_HISTORY          # Write the history file in the ":start:elapsed;command" format.
setopt HIST_EXPIRE_DUPS_FIRST    # Expire duplicate entries first when trimming history.
setopt HIST_FIND_NO_DUPS         # Don't display duplicates of a line previously found.
setopt HIST_IGNORE_ALL_DUPS      # If a new command line being added to the history list duplicates an older one, the older command is removed  from  the  list.
setopt HIST_IGNORE_SPACE         # Don't record an entry starting with a space.
setopt HIST_REDUCE_BLANKS        # Remove superfluous blanks before recording entry.
setopt HIST_VERIFY               # Don't execute immediately upon history expansion.
setopt SHARE_HISTORY             # Share history between all sessions.

# Completion
fpath+=$HOME/.config/completions
zmodload zsh/complist
autoload -U compinit; compinit
# Matching: first exact match, then uppercase from lowercase and match on the left side (bar -> foobar)
zstyle ':completion:*' matcher-list '' 'l:|=* r:|=* m:{a-z}={A-Z}'
zstyle ':completion:*' menu select
zstyle ':completion:*' special-dirs ..
zstyle ':completion:*:default' list-colors --color=auto
zstyle ':completion:*:messages' format ' %F{purple} -- %d --%f'
zstyle ':completion:*:warnings' format '%F{red}No matches for:%f %d'

# separate matches into groups
zstyle ':completion:*:descriptions'    format '%F{blue}completing %B%d%b%f'
zstyle ':completion:*'                 group-name ''

set_window_title() {
	echo -ne "\033]0; $HOST ${PWD/$HOME/~} \007"
}
precmd_functions+=(set_window_title)

osc7() {
	local LC_ALL=C
	export LC_ALL

	setopt localoptions extendedglob
	input=( ${(s::)PWD} )
	uri=${(j::)input/(#b)([^A-Za-z0-9_.\!~*\'\(\)-\/])/%${(l:2::0:)$(([##16]#match))}}
	print -n "\e]7;file://${HOST}${uri}\e\\"
}
chpwd_functions+=(osc7)

osc133() {
	print -Pn "\e]133;A\e\\"
}
precmd_functions+=(osc133)

source "$ZSH_PLUGINS/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"
source "$ZSH_PLUGINS/zsh-history-substring-search/zsh-history-substring-search.zsh"

source ~/.config/aliases
source ~/.config/env

# Load stuff
for config_file ($HOME/.config/zsh/*.zsh); do
	source $config_file
done

eval "$(starship init zsh)"
